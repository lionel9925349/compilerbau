


package de.thm.mni.compilerbau.phases._02_03_parser;

import java.util.List;
import java.util.LinkedList;
import java_cup.runtime.*;
import de.thm.mni.compilerbau.phases._01_scanner.Scanner;
import de.thm.mni.compilerbau.absyn.*;
import de.thm.mni.compilerbau.table.Identifier;
import de.thm.mni.compilerbau.utils.SplError;
import de.thm.mni.compilerbau.CommandLineOptions;

parser code {:
  public CommandLineOptions options = null;

  public void syntax_error(Symbol currentToken) {
    throw SplError.SyntaxError(new Position(currentToken.left, currentToken.right), ((Scanner)getScanner()).yytext());
  }

  /**
   * @return An empty list. (Uses LinkedList)
   */
  public static <E> List<E> nil() {
      return new LinkedList<E>();
  }

  /**
   * Constructs a list from a head and a tail without modifying the tail (Uses LinkedList).
   * Use this as a helper if you don't want to deal with constructing lists manually.
   *
   * @param head The head of the list
   * @param tail The tail of the list
   * @return A list containing the head element and all elements of tail
   */
  public static <E> List<E> cons(E head, List<E> tail) {
    List<E> copy = new LinkedList(tail);
    copy.add(0, head);
    return copy;
  }
:}

/* Terminals/Tokens */
terminal LBRACK, RBRACK, LPAREN, RPAREN, LCURL, RCURL;         //Braces, parenthesis, brackets
terminal LT, LE, GT, GE, NE, EQ;                               //Comparison operators
terminal PLUS, MINUS, STAR, SLASH;                             //Arithmetic operators
terminal ASGN, COMMA, COLON, SEMIC;                            //Other delimiters
terminal TYPE, PROC, ARRAY, OF, REF, VAR, IF, ELSE, WHILE;     //Keywords

/* Non-terminals */
terminal Identifier IDENT;    //Identifiers
terminal Integer INTLIT;      //Integer literals

non terminal Program program;

non terminal  ArrayAccess arrayAccess;
non terminal  ArrayTypeExpression arrayTypeExpession;
non terminal  AssignStatement assignStatement;
non terminal  Expression binaryExpression;
non terminal  CallStatement callStatement;
non terminal  CompoundStatement compoudStatement;
non terminal  GlobalDeclaration globalDeclaration;
non terminal  IfStatement ifStatement;
non terminal  EmptyStatement emptystatement;
non terminal  NamedTypeExpression nameTypeExpression;
non terminal  NamedVariable  namedVariable;
non terminal  ParameterDeclaration  parameterDeclaration;
non terminal  ProcedureDeclaration procedureDeclaration;
non terminal  List<Statement> statementList;
non terminal  TypeDeclaration typeDeclaration;
non terminal  TypeExpression typeExpression;
non terminal  Variable variable;
non terminal  VariableDeclaration variableDeclaration;
non terminal  Expression variableExpression;
non terminal  WhileStatement whileStatement;


non terminal  Expression expr1 ;
non terminal  Expression expr2 ;
non terminal  Expression expr3 ;
non terminal  Expression expr25;
non terminal  Expression expression;
non terminal  Statement statement;
non terminal  List<VariableDeclaration> variableDeclarationList;
non terminal  List<Expression> argumentList;
non terminal  Expression argument;
non terminal  List<Expression> arguments;
non terminal  List<ParameterDeclaration> parameterDeclarationList;
non terminal  List<ParameterDeclaration> parameterDeclarations;
non terminal  List<GlobalDeclaration> globalDeclarationList;
non terminal  List<GlobalDeclaration> globalDeclarations;


start with program;

program ::= globalDeclarations:gd {: RESULT = new Program(new Position(gdleft,gdright),gd); :};


// m1 := expr1 - expr2
// m1 := expr1 + expr2 - expr2
// m1 := expr1 + expr2 - expr2 *
expr1 ::= expr1:lhs PLUS expr2:rhs  {: RESULT = new BinaryExpression(new Position(lhsleft,lhsright)  ,BinaryExpression.Operator.ADD,lhs,rhs); :}
        | expr1:lhs MINUS expr2:rhs  {: RESULT = new BinaryExpression(new Position(lhsleft,lhsright)  ,BinaryExpression.Operator.SUB,lhs,rhs); :}
        | expr2:lhs {: RESULT = lhs; :} ;
expr2 ::= expr2:lhs STAR expr3:rhs {: RESULT = new BinaryExpression(new Position(lhsleft,lhsright)  ,BinaryExpression.Operator.MUL,lhs,rhs); :}
        | expr2:lhs SLASH expr3:rhs  {: RESULT = new BinaryExpression(new Position(lhsleft,lhsright)  ,BinaryExpression.Operator.DIV,lhs,rhs); :}
        | expr3: ex3 {: RESULT = ex3; :};

//        | expr25: ex25 {: RESULT = ex25; :} ;
//expr25 ::= expr3: ex3 {: RESULT = ex3; :}
        //| MINUS:min expr3:rhs  {: RESULT = new BinaryExpression(new Position(rhsleft,rhsright)  ,BinaryExpression.Operator.SUB,new IntLiteral(new Position(minleft,minright),0),rhs); :};

expr3 ::= LPAREN expr1:ex1  RPAREN {: RESULT = ex1; :}
        | MINUS:min expr3:rhs  {: RESULT = new BinaryExpression(new Position(rhsleft,rhsright)  ,BinaryExpression.Operator.SUB,new IntLiteral(new Position(minleft,minright),0),rhs); :}
        | variable: var {: RESULT = new VariableExpression(new Position(varleft, varright), var) ; :}
        | INTLIT:in {: RESULT = new IntLiteral(new Position(inleft,inright), in); :}; //fragen

binaryExpression ::= expr1:exp {: RESULT = exp; :};

variableExpression ::= expr1:exp {: RESULT = exp; :};

globalDeclarations ::= globalDeclarationList:gdl {: RESULT = gdl; :}
                        | {: RESULT = nil(); :} ;

globalDeclarationList ::= globalDeclaration:gd  globalDeclarationList:gdl {: RESULT = cons(gd, gdl); :}
                        | globalDeclaration:gd {: RESULT = cons(gd, nil()); :};

globalDeclaration ::= procedureDeclaration:pd {: RESULT = pd; :}
                    | typeDeclaration:typDec {: RESULT = typDec; :} ;

variable ::= arrayAccess:arr {: RESULT = arr; :} | namedVariable:nam {: RESULT = nam; :};
arrayAccess ::= variable:var LBRACK expr1:ex RBRACK {: RESULT = new ArrayAccess ( new Position(varleft,varright),var, ex); :};
namedVariable::=IDENT:id {: RESULT = new NamedVariable ( new Position(idleft,idright), id ); :};

typeDeclaration ::= TYPE IDENT:id EQ typeExpression:typEx SEMIC {: RESULT = new TypeDeclaration(new Position(idleft,idright),id,typEx );:};

typeExpression ::=  arrayTypeExpession:arrTypEx {: RESULT = arrTypEx; :}| nameTypeExpression:nameTypEx {: RESULT = nameTypEx; :};
nameTypeExpression::= IDENT:id {: RESULT = new NamedTypeExpression(new Position(idleft,idright),id); :};
arrayTypeExpession::= ARRAY:pos  LBRACK INTLIT:size  RBRACK OF typeExpression:typEx {: RESULT = new ArrayTypeExpression(new Position(posleft,posright),typEx,size); :} ;

ifStatement ::= IF LPAREN expression:ex RPAREN statement:stat {: RESULT = new IfStatement(new Position(exleft,exright),ex,stat,new EmptyStatement(new Position(exleft,exright))); :}
//zu fragen statl
            | IF LPAREN expression:ex RPAREN statement:stat ELSE statement:statl {: RESULT = new IfStatement(new Position(exleft,exright),ex,stat,statl); :} ;

statementList ::= statement:stat statementList:statl {: RESULT = cons(stat, statl); :}
                | {: RESULT = nil(); :};

statement ::=  assignStatement:assg {: RESULT = assg; :}
               | callStatement:call {: RESULT = call; :}
               | compoudStatement:comp {: RESULT = comp; :}
               | emptystatement:emp {: RESULT = emp; :}
               | ifStatement:ifs {: RESULT = ifs; :}
               | whileStatement:whis {: RESULT = whis; :};

compoudStatement ::= LCURL statementList:statl RCURL {: RESULT = new CompoundStatement(new Position(statlleft,statlright), statl); :};
emptystatement::= SEMIC:sem {: RESULT = new EmptyStatement(new Position(semleft,semright)); :};
callStatement ::= IDENT:id LPAREN arguments:args RPAREN SEMIC {: RESULT = new CallStatement(new Position(idleft,idright),id,args); :};
                //    public CallStatement(Position position, Identifier procedureName, List<Expression> arguments)
                // est ce qu on doit mettre la liste sur noptre call
argumentList ::= argument:arg COMMA argumentList:argl {: RESULT = cons(arg, argl); :}
                |  argument:arg {: RESULT = cons(arg, nil()); :} ;

arguments ::=  argumentList:argL {: RESULT = argL; :}
            | {: RESULT = nil(); :};
argument ::= expr1:exp {: RESULT = exp; :} ;

assignStatement ::= variable:var ASGN expr1:ex1 SEMIC {: RESULT = new AssignStatement (new Position(varleft,varright),var,ex1); :};


expression ::= expr1:lhs EQ expr1:rhs {: RESULT = new BinaryExpression(new Position(lhsleft,lhsright)  ,BinaryExpression.Operator.EQU,lhs,rhs); :}
            | expr1:lhs GT expr1:rhs    {: RESULT = new BinaryExpression(new Position(lhsleft,lhsright)  ,BinaryExpression.Operator.GRT,lhs,rhs); :}
            | expr1:lhs  GE expr1: rhs  {: RESULT = new BinaryExpression(new Position(lhsleft,lhsright)  ,BinaryExpression.Operator.GRE,lhs,rhs); :}
            | expr1:lhs LE expr1:rhs   {: RESULT = new BinaryExpression(new Position(lhsleft,lhsright)  ,BinaryExpression.Operator.LSE,lhs,rhs); :}
            | expr1:lhs LT expr1:rhs    {: RESULT = new BinaryExpression(new Position(lhsleft,lhsright)  ,BinaryExpression.Operator.LST,lhs,rhs); :}
            | expr1:lhs NE expr1:rhs  {: RESULT = new BinaryExpression(new Position(lhsleft,lhsright)  ,BinaryExpression.Operator.NEQ,lhs,rhs); :}
            | expr1:lhs {: RESULT = lhs; :} ;


whileStatement ::= WHILE LPAREN expression:exp RPAREN statement:stmL {: RESULT = new WhileStatement(new Position(expleft,expright),exp,stmL); :};

procedureDeclaration ::= PROC IDENT:id LPAREN parameterDeclarations:pardec
                     RPAREN LCURL variableDeclarationList:varDec statementList:state RCURL {: RESULT = new ProcedureDeclaration(new Position(pardecleft,pardecright),id, pardec, varDec, state);:};

parameterDeclarations ::= parameterDeclarationList:parDecL {: RESULT = parDecL; :}
                        | {: RESULT = nil(); :};
parameterDeclarationList ::= parameterDeclaration:parDec COMMA parameterDeclarationList:parDecL {: RESULT = cons(parDec, parDecL); :}
                            | parameterDeclaration:parDec {: RESULT = cons(parDec, nil()); :};

parameterDeclaration ::= IDENT:id COLON typeExpression:typEx {: RESULT = new ParameterDeclaration(new Position(idleft,idright),id,typEx,false); :}
                        | REF:ref IDENT:id COLON typeExpression:typEx {: RESULT = new ParameterDeclaration(new Position(refleft,refright),id,typEx, true); :};//zu fragen

variableDeclarationList ::= variableDeclaration:varDec variableDeclarationList:varDecL {: RESULT = cons(varDec,varDecL); :}
                          | {: RESULT = nil(); :};

variableDeclaration ::= VAR:var IDENT:id COLON typeExpression:typEx SEMIC {: RESULT = new VariableDeclaration(new Position(varleft,varright),id,typEx); :};

